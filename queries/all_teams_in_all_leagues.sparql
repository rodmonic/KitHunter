SELECT ?countryLabel ?level ?league ?leagueLabel ?team ?teamLabel
WHERE 
{
  {   
  ?league wdt:P31 wd:Q15991303. 
  ?league p:P361 ?statement.   
  ?statement  ps:P361 ?league_system;
              pq:P3983 ?level.
  ?league_system wdt:P17 ?country.
  ?pq_qual wikibase:qualifier pq:P3983.  
  
    ?team wdt:P118 ?league;  # Team plays in the league
        wdt:P31 wd:Q476028   
    
  }
  UNION
   {
  ?league wdt:P31 wd:Q15991303;   # Instance of football league
          wdt:P3983 ?level;  # Direct sports league level assignment
          wdt:P17 ?country.
  # Linking teams to the league
  ?team wdt:P118 ?league;  # Team plays in the league
        wdt:P31 wd:Q476028 
  }
  
  # Filter for levels 1, 2, or 3
  FILTER(?level IN (1, 2, 3))
  
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}

ORDER BY ?countryLabel ?level ?league ?team